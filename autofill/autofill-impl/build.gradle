/*
 * Copyright (c) 2021 DuckDuckGo
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.anvil)
}

apply from: "$rootProject.projectDir/gradle/android-library.gradle"

ext {
    autofillDisableAuthRequirementBuildFlag = "autofill-disable-auth-requirement"
}

dependencies {
    implementation project(path: ':app-build-config-api')
    implementation project(path: ':privacy-config-api')
    implementation project(path: ':feature-toggles-api')
    implementation project(path: ':di')
    implementation project(path: ':common-utils')
    implementation project(path: ':common-ui')
    implementation project(path: ':autofill-api')
    implementation project(path: ':browser-api')
    implementation project(path: ':autofill-store')
    implementation project(path: ':statistics')
    testImplementation project(path: ':autofill-test')
    implementation project(path: ':sync-api')
    implementation project(path: ':navigation-api')
    implementation project(':user-agent-api')
    implementation project(':new-tab-page-api')
    implementation project(':data-store-api')

    anvil project(path: ':anvil-compiler')
    implementation project(path: ':anvil-annotations')

    implementation libs.androidx.appCompat
    implementation libs.material
    implementation libs.androidx.constraintLayout
    implementation libs.timber

    implementation libs.kotlin.coroutines.core
    implementation libs.androidx.fragment.ktx
    implementation libs.androidx.webkit

    implementation "androidx.datastore:datastore-preferences:_"

    implementation libs.retrofit.converter.moshi
    implementation libs.moshi.kotlin
    implementation libs.moshi.adapters
    implementation libs.dagger2
    implementation libs.androidx.core.ktx
    implementation libs.androidx.workManager.ktx
    implementation libs.androidx.room.ktx
    implementation libs.androidx.biometric

    implementation "net.zetetic:android-database-sqlcipher:_"

    // Testing dependencies
    testImplementation libs.mockito.kotlin
    testImplementation libs.junit4
    testImplementation libs.androidx.core.ktx
    testImplementation AndroidX.test.ext.junit
    testImplementation libs.androidx.test.runner
    testImplementation Testing.robolectric
    testImplementation libs.turbine
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:_"

    testImplementation project(path: ':common-test')

    testImplementation (libs.kotlin.coroutines.test) {
        // https://github.com/Kotlin/kotlinx.coroutines/issues/2023
        // conflicts with mockito due to direct inclusion of byte buddy
        exclude group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-debug"
    }

    coreLibraryDesugaring Android.tools.desugarJdkLibs
}

android {
    anvil {
        generateDaggerFactories = true // default is false
    }
    lint {
        baseline file("lint-baseline.xml")
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    namespace 'com.duckduckgo.autofill.impl'
    compileOptions {
        coreLibraryDesugaringEnabled = true
    }
    defaultConfig {
        if (project.hasProperty("$autofillDisableAuthRequirementBuildFlag")) {
            buildConfigField "boolean", "AUTH_REQUIRED", "false"
        } else {
            buildConfigField "boolean", "AUTH_REQUIRED", "true"
        }
    }
}

tasks.register('installForAutofillTesting', Exec) {
    commandLine "$rootDir/gradlew", ':app:installInternalRelease', "-P$autofillDisableAuthRequirementBuildFlag"
}

tasks.register('autofillTestLocal', Exec) {
    commandLine 'maestro', 'test', '--include-tags', 'autofillNoAuthTests', "$rootDir/.maestro"
    dependsOn 'installForAutofillTesting'
}