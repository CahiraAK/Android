/*
 * Copyright (c) 2021 DuckDuckGo
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.anvil)
}

apply from: "$rootProject.projectDir/gradle/android-library.gradle"

ext {
    syncDisableAuthRequirementBuildFlag = "sync-disable-auth-requirement"
}

android {
    namespace 'com.duckduckgo.sync.impl'
}

dependencies {
    implementation project(path: ':di')
    implementation project(':browser-api')
    implementation project(path: ':common-utils')
    implementation project(path: ':common-ui')
    implementation project(path: ':sync-api')
    implementation project(path: ':sync-store')
    implementation project(path: ':sync-lib')
    implementation project(path: ':saved-sites-api')
    implementation project(':feature-toggles-api')
    implementation project(':navigation-api')
    implementation project(':remote-messaging-api')

    implementation project(path: ':app-build-config-api')
    implementation project(path: ':privacy-config-api')
    implementation project(':statistics')

    anvil project(path: ':anvil-compiler')
    implementation project(path: ':anvil-annotations')

    implementation libs.androidx.appCompat
    implementation libs.material
    implementation libs.timber

    implementation libs.kotlin.coroutines.core
    implementation libs.androidx.fragment.ktx
    implementation libs.androidx.biometric

    implementation libs.retrofit.converter.moshi
    implementation libs.moshi.kotlin
    implementation libs.dagger2
    implementation libs.androidx.core.ktx
    implementation('com.journeyapps:zxing-android-embedded:_') { transitive = false }
    implementation 'com.google.zxing:core:_'

    // Shimmer
    implementation libs.shimmer

    // WorkManager
    implementation libs.androidx.workManager.ktx
    testImplementation libs.androidx.workManager.testing

    // Room
    implementation libs.androidx.room.runtime
    implementation libs.androidx.room.ktx

    // Testing dependencies
    testImplementation libs.mockito.kotlin
    testImplementation libs.junit4
    testImplementation libs.androidx.core.ktx
    testImplementation libs.androidx.test.ext.junit
    testImplementation libs.androidx.test.rules
    testImplementation libs.androidx.test.runner
    testImplementation libs.robolectric
    testImplementation libs.turbine
    testImplementation libs.androidx.arch.core.testing
    testImplementation libs.kotlin.reflect
    testImplementation 'org.json:json:_'

    testImplementation project(path: ':common-test')

    testImplementation (libs.kotlin.coroutines.test) {
        // https://github.com/Kotlin/kotlinx.coroutines/issues/2023
        // conflicts with mockito due to direct inclusion of byte buddy
        exclude group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-debug"
    }

    coreLibraryDesugaring libs.android.desugarJDKLibs
}

android {
    anvil {
        generateDaggerFactories = true // default is false
    }
    lint {
        baseline file("lint-baseline.xml")
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled = true
    }
    defaultConfig {
        if (project.hasProperty("$syncDisableAuthRequirementBuildFlag")) {
            buildConfigField "boolean", "AUTH_REQUIRED", "false"
        } else {
            buildConfigField "boolean", "AUTH_REQUIRED", "true"
        }
    }
}

tasks.register('installForSyncTesting', Exec) {
    commandLine "$rootDir/gradlew", ':app:installInternalDebug', "-P$syncDisableAuthRequirementBuildFlag"
}

